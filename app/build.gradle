/*
 * Copyright (c) 2016 Martin Bella. All rights reserved.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 *     http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def app = "Aneko"
def versions = new Properties();
versions.load(file("${project.rootDir}/version.properties").newDataInputStream())

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.1'
    
    defaultConfig {
        applicationId 'net.orange_box.aneko'
        
        minSdkVersion 15
        targetSdkVersion 24
        
        versionCode genVersionCode()
        versionName genVersionName(versions)
        
        archivesBaseName = "${app.replace(' ', '_')}-${versionName}"
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    
    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
    }
    
    signingConfigs {
        release.initWith(debug)
    }
    
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles 'proguard.cfg'
            
            signingConfig signingConfigs.release
        }
    }
    
    // workaround for #1
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:$versionSupport"
    compile "com.android.support:support-annotations:$versionSupport"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$versionKotlin"
}

play {
    jsonFile = file("${project.rootDir}/play.json")
    track = 'alpha'
}

// tasks

task signingConfig << {
    def kpass = project.hasProperty('keystorePass') ? keystorePass : System.getenv('KEYSTORE_PASS')
    def aname = project.hasProperty('aliasName') ? aliasName : System.getenv('ALIAS_NAME')
    def apass = project.hasProperty('aliasPass') ? aliasPass : System.getenv('ALIAS_PASS')
    
    if (kpass != null && aname != null && apass != null) {
        android.signingConfigs.release.storeFile = file("${project.rootDir}/release.keystore")
        android.signingConfigs.release.storePassword = kpass
        android.signingConfigs.release.keyAlias = aname
        android.signingConfigs.release.keyPassword = apass
    } else {
        logger.warn('Signing APK with debug key')
    }
}

tasks.whenTaskAdded {
    if (it.name.equals('packageRelease')) {
        it.dependsOn 'signingConfig'
    }
}

// utils

def genVersionCode() {
    return Integer.parseInt(System.getenv('TRAVIS_BUILD_NUMBER') ?: '0')
}

def genVersionName(Properties props) {
    return "${props['major']}.${props['minor']}.${props['patch']}.${System.getenv('TRAVIS_BUILD_NUMBER') ?: '0'}"
}
